apply plugin: 'com.android.application'

def getVersionTag = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionPatch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', getVersionTag() + '..HEAD', '--no-merges', '--count'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionName = { ->
    return getVersionTag() + "." + getVersionPatch()
}

def getVersionCode = { ->
    return getVersionName().replace(".", "").toInteger()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.example.greetings"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode = getVersionCode()
        versionName = getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}